plugins {
    id 'kotlin'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

group = lrouterGroup
version = lrouterVersion
project.ext.artifactId = 'plugin'

sourceSets {
    main.java.srcDirs += "$buildDir/generated/build/com/aleyn/plugin"
}

def map = new HashMap<String, String>()
map.put('version', "$lrouterVersion")
map.put('routerDepend', "$lrouterGroup:core:")
map.put('processorDepend', "$lrouterGroup:processor:")

tasks.register('genVersion', Copy) {
    from 'src/main/build'
    into "$buildDir/generated/build/com/aleyn/plugin"
    expand(map)
    filteringCharset = 'UTF-8'
}

compileKotlin.dependsOn('genVersion')

tasks.register('copyJar', Copy) {
    from "${buildDir}/libs/plugin-${lrouterVersion}.jar"
    into "../plugins"
    filteringCharset = 'UTF-8'
    rename { String name ->
        name.replace("-${lrouterVersion}", '')
    }
}


jar.finalizedBy('copyJar')

dependencies {
    implementation gradleApi()
    compileOnly libs.gradle
    compileOnly libs.symbol.processing.gradle.plugin
    implementation libs.asmCore
    implementation libs.asm.commons
    implementation libs.gson
}

apply from: '../publishing.gradle'